// gradle task and plugin configuration
buildscript {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'

// global settings
group 'io.cloudwall'
version '0.1.0'
sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10
repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'https://dl.bintray.com/io-cloudwall/cloudwall-maven' }
}

dependencies {
    compile 'args4j:args4j:2.33',
            'com.google.code.findbugs:jsr305:3.0.2',
            'com.google.dagger:dagger:2.16',
            'com.typesafe:config:1.3.3',
            'io.netty:netty-codec-http:4.1.28.Final',
            'org.apache.avro:avro:1.8.2',
            'org.apache.logging.log4j:log4j-api:2.11.1',
            'org.apache.logging.log4j:log4j-core:2.11.1',
            'org.fusesource.jansi:jansi:1.17.1',
            'org.redisson:redisson:3.7.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    testCompile 'junit:junit:4.12',
                'org.hamcrest:hamcrest-library:1.4-atlassian-1',
                'org.mockito:mockito-core:2.21.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

compileJava {
	doFirst {
		options.compilerArgs = [
				'--add-modules', 'jdk.incubator.httpclient'
		]
	}
}

javadoc {
    options.addStringOption('-add-modules', 'jdk.incubator.httpclient')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
   	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	configurations = ['archives']
	pkg {
		repo = 'cloudwall-maven'
		name = 'wisp'
		userOrg = 'io-cloudwall'
        desc = 'Cloud Wall Graph Algorithms'
        websiteUrl = 'https://github.com/cloudwall/wisp'
        issueTrackerUrl = 'https://github.com/cloudwall/wisp/issues'
		vcsUrl = 'https://github.com/cloudwall/wisp.git'
        licenses = ['Apache-2.0']

        githubRepo = 'cloudwall/wisp'
        githubReleaseNotesFile = 'README.md'
        
        version {
            name = project.version
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': "io.cloudwall:wisp:$project.version"]
        }
	}
}
